buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

}

def flinkVersion = rootProject.flinkVersion

description = """you know where to add your functions in dagger!"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


configurations {
    minimalFunctionsJar
    dependenciesFunctionsJar

    minimalFunctionsJar.extendsFrom runtime
    compileOnly.extendsFrom minimalFunctionsJar
    compileOnly.extendsFrom dependenciesFunctionsJar
    testCompile.extendsFrom compileOnly
}

sourceSets {
    main.compileClasspath += configurations.minimalFunctionsJar
    main.compileClasspath += configurations.dependenciesFunctionsJar
    main.runtimeClasspath += configurations.minimalFunctionsJar
    main.runtimeClasspath += configurations.dependenciesFunctionsJar

    test.compileClasspath += configurations.minimalFunctionsJar
    test.compileClasspath += configurations.dependenciesFunctionsJar
    test.runtimeClasspath += configurations.minimalFunctionsJar
    test.runtimeClasspath += configurations.dependenciesFunctionsJar
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.8'
    
    compileOnly project(path: ':dagger-common', configuration: 'minimalCommonJar')
    compileOnly project(path: ':dagger-common', configuration: 'dependenciesCommonJar')
    compileOnly 'org.apache.flink:flink-streaming-java_2.11:' + flinkVersion
    compileOnly 'org.apache.flink:flink-connector-kafka_2.11:' + flinkVersion
    compileOnly group: 'org.scala-lang', name: 'scala-library', version: '2.10.6'
    compileOnly group: 'org.apache.flink', name: 'flink-clients_2.11', version: flinkVersion
    compileOnly group: 'org.apache.flink', name: 'flink-table', version: flinkVersion
    compileOnly group: 'org.apache.flink', name: 'flink-table-api-java-bridge_2.11', version: flinkVersion
    compileOnly group: 'org.apache.flink', name: 'flink-streaming-java_2.11', version: flinkVersion
    compileOnly group: 'org.apache.flink', name: 'flink-metrics-dropwizard', version: flinkVersion
    compileOnly 'com.gotocompany:stencil:0.4.0'

    dependenciesFunctionsJar 'com.github.davidmoten:geo:0.7.6'
    dependenciesFunctionsJar 'org.apache.flink:flink-python_2.11:' + flinkVersion
    dependenciesFunctionsJar group: 'org.apache.commons', name: 'commons-jexl3', version: '3.1'
    dependenciesFunctionsJar group: 'org.isuper', name: 's2-geometry-library-java', version: '0.0.1'
    dependenciesFunctionsJar group: 'com.google.cloud', name: 'google-cloud-storage', version: '1.67.0'
    
    testImplementation project(':dagger-common').sourceSets.test.output
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'org.mockito:mockito-core:2.0.99-beta'
    testImplementation gradleTestKit()
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}

test {
    finalizedBy jacocoTestReport
    testLogging {
        events "passed", "skipped", "failed"
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
        csv.enabled false
    }
    finalizedBy jacocoTestCoverageVerification
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.90
            }
        }
    }
}
